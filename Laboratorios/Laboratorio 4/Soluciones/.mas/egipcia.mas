/ MARIE ASSEMBLY PROGRAM - LAB2C
/ AUTHOR: JUAN ESTEBAN SÁNCHEZ GARCÍA
/ EGYPTIAN FRACTION DECOMPOSITION

            ORG 100

/ ==============================================
/ READ NUMBER OF TEST CASES
/ ==============================================
    INPUT
    STORE CASE_COUNT

/ ==============================================
/ MAIN LOOP: PROCESS EACH CASE
/ ==============================================
LOOP_CASES, LOAD CASE_COUNT
    SKIPCOND 400           / IF CASE_COUNT == 0, END PROGRAM
    JUMP PROCESS_CASE
    JUMP END_PROGRAM

/ ==============================================
/ READ INPUTS AND CALL EGYPT SUBROUTINE
/ ==============================================
PROCESS_CASE, INPUT
    STORE NUM_A
    INPUT
    STORE NUM_B
    JNS EGYPT              / CALL EGYPTIAN DECOMPOSITION

    / PRINT 0 TO MARK END OF CASE
    LOAD ZERO
    OUTPUT

    / DECREMENT CASE COUNT
    LOAD CASE_COUNT
    SUBT ONE
    STORE CASE_COUNT
    JUMP LOOP_CASES

/ ==============================================
/ END PROGRAM
/ ==============================================
END_PROGRAM, HALT


/ =====================================================
/ SUBROUTINE: EGYPTIAN FRACTION DECOMPOSITION (EGYPT)
/ =====================================================
EGYPT, HEX 0
BEGIN_EGYPT, LOAD NUM_A
    SKIPCOND 400
    JUMP CHECK_B_ZERO
    JUMP END_EGYPT

CHECK_B_ZERO, LOAD NUM_B
    SKIPCOND 400
    JUMP CASE1
    JUMP END_EGYPT

/ CASE 1: IF NUM_B DIVIDES NUM_A EXACTLY
CASE1, LOAD ZERO
    STORE DIV_QUOT
    STORE DIV_REM
    LOAD NUM_B
    STORE DIV_A
    LOAD NUM_A
    STORE DIV_B
    JNS DIVIDE
    STORE TEMP_REM
    LOAD TEMP_REM
    SKIPCOND 400
    JUMP CASE2
    LOAD DIV_QUOT
    OUTPUT
    JUMP END_EGYPT

/ CASE 2: IF NUM_A DIVIDES NUM_B EXACTLY
CASE2, LOAD ZERO
    STORE DIV_QUOT
    STORE DIV_REM
    LOAD NUM_A
    STORE DIV_A
    LOAD NUM_B
    STORE DIV_B
    JNS DIVIDE
    STORE TEMP_REM
    LOAD TEMP_REM
    SKIPCOND 400
    JUMP CASE3
    LOAD DIV_QUOT
    OUTPUT
    JUMP END_EGYPT

/ CASE 3: GENERAL STEP (NUM_A >= NUM_B)
CASE3, LOAD NUM_A
    SUBT NUM_B
    SKIPCOND 800
    JUMP CASE4
    LOAD ZERO
    STORE DIV_QUOT
    STORE DIV_REM
    LOAD NUM_A
    STORE DIV_A
    LOAD NUM_B
    STORE DIV_B
    JNS DIVIDE
    STORE NUM_A
    LOAD DIV_QUOT
    OUTPUT
    JUMP BEGIN_EGYPT

/ CASE 4: NUM_A < NUM_B
CASE4, LOAD ZERO
    STORE DIV_QUOT
    STORE DIV_REM
    STORE MULT_ACC
    LOAD NUM_B
    STORE DIV_A
    LOAD NUM_A
    STORE DIV_B
    JNS DIVIDE
    LOAD DIV_QUOT
    ADD ONE
    STORE N
    OUTPUT

    / NUM_A = NUM_A * N - NUM_B
    LOAD NUM_A
    STORE MULT_A
    LOAD N
    STORE MULT_B
    JNS MULTIPLY
    SUBT NUM_B
    STORE NUM_A

    / NUM_B = NUM_B * N
    LOAD ZERO
    STORE MULT_ACC
    LOAD NUM_B
    STORE MULT_A
    LOAD N
    STORE MULT_B
    JNS MULTIPLY
    STORE NUM_B

    JUMP BEGIN_EGYPT

END_EGYPT, JUMPI EGYPT


/ =====================================================
/ SUBROUTINE: INTEGER DIVISION (DIVIDE)
/ =====================================================
DIVIDE, HEX 0
LOOP_DIV, LOAD DIV_A
    SKIPCOND 800
    JUMP FIX_DIV
    LOAD DIV_A
    SUBT DIV_B
    STORE DIV_A
    LOAD DIV_QUOT
    ADD ONE
    STORE DIV_QUOT
    JUMP LOOP_DIV

FIX_DIV, LOAD DIV_A
    SKIPCOND 000
    JUMP END_DIV
    LOAD DIV_QUOT
    SUBT ONE
    STORE DIV_QUOT
    LOAD DIV_A
    ADD DIV_B
    STORE DIV_REM
    JUMP END_DIV

END_DIV, LOAD DIV_REM
    JUMPI DIVIDE


/ =====================================================
/ SUBROUTINE: MULTIPLICATION (MULTIPLY)
/ =====================================================
MULTIPLY, HEX 0
LOOP_MULT, LOAD MULT_A
    SKIPCOND 800
    JUMP END_MULT
    LOAD MULT_ACC
    ADD MULT_B
    STORE MULT_ACC
    LOAD MULT_A
    SUBT ONE
    STORE MULT_A
    JUMP LOOP_MULT

END_MULT, LOAD MULT_ACC
    JUMPI MULTIPLY


/ =====================================================
/ VARIABLES
/ =====================================================
CASE_COUNT, DEC 0
NUM_A,      DEC 0
NUM_B,      DEC 0
TEMP_REM,   DEC 0
N,          DEC 0

MULT_A,     DEC 0
MULT_B,     DEC 0
MULT_ACC,   DEC 0

DIV_A,      DEC 0
DIV_B,      DEC 0
DIV_QUOT,   DEC 0
DIV_REM,    DEC 0

ONE,        DEC 1
ZERO,       DEC 0

            END
